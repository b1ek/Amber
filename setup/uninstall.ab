import { exit } from "std/env"
import { includes } from "std/array"
import { get_arch, get_place, get_distro, get_bins_folder } from "./shared.ab"

fun install_via_pkg(args: [Text], distro: Text): Null {
    if includes(args, "--no-distro-install") {
        return null
    }
    if includes(args, "--user") {
        return null
    }

    if distro == "nix" {
        echo "Refusing to uninstall on nix"
        echo ""
        echo "It seems that you are using nix and have probably installed it via the weird nix way"
        echo "Pass --no-distro-install if you want to try uninstall it anyway"
        exit(1)
    }

    if distro == "arch" {
        echo "Arch detected, trying to uninstall via pacman..."
        echo "Pass --no-distro-install to uninstall without a package manager"
        
        unsafe $sudo pacman -R amber-bash --noconfirm$
        exit(status)
    }

    if distro == "snap" {
        echo "Snap detected, trying to uninstall via snap..."
        echo "Pass --no-distro-install to uninstall without a package manager"

        unsafe $sudo snap uninstall amber-bash$
        exit(status)
    }
}

main(args) {
    let arch = get_arch()
    let distro = get_distro()
    if distro != "" {
        install_via_pkg(args, distro)
    }

    let user_only_install = includes(args, "--user")
    let place = get_place(user_only_install)
    let bins_folder = get_bins_folder(user_only_install)

    unsafe $test -d "{place}" > /dev/null$

    if status == 0 {
        let sudo = user_only_install then "" else "sudo"
        ${sudo} rm -rf "{place}"$ failed {
            echo "Failed to remove Amber from {place}"
            echo "Make sure root has the correct permissions to access this directory"
            exit(1)
        }
        ${sudo} rm "{bins_folder}/amber"$ failed {
            echo "Failed to remove Amber symlink from {bins_folder}"
            echo "Make sure root has the correct permissions to access this directory"
            exit(1)
        }
        echo "Uninstalled Amber successfully ðŸŽ‰"
    }
    else {
        echo "Amber is not installed"
    }
}
